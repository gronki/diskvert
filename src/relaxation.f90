module relaxation

  use iso_fortran_env, only: r64 => real64, uerr => error_unit
  use ieee_arithmetic, only: ieee_is_normal

  use slf_cgs
  use globals
  use slf_ramps

  implicit none

  interface
    pure subroutine funout_t(z, Y, YY)
      import r64
      real(r64), intent(in) :: z
      real(r64), intent(in), dimension(:) :: Y
      real(r64), intent(out), dimension(:) :: YY
    end subroutine
    pure subroutine fun0_t(z, Y, F, A, M)
      import r64
      real(r64), intent(in) :: z
      real(r64), intent(in), dimension(:) :: Y
      real(r64), intent(in), dimension(:,:) :: F
      real(r64), intent(out), dimension(:) :: A
      real(r64), intent(out), dimension(:,:) :: M
    end subroutine
    pure subroutine fun1_t(z, Y, D, F, A, MY, MD)
      import r64
      real(r64), intent(in) :: z
      real(r64), intent(in), dimension(:) :: Y,D
      real(r64), intent(in), dimension(:,:) :: F
      real(r64), intent(out), dimension(:) :: A
      real(r64), intent(out), dimension(:,:) :: MY,MD
    end subroutine
    pure subroutine fun2_t(z, Y, D, D2, F, A, MY, MD, MD2)
      import r64
      real(r64), intent(in) :: z
      real(r64), intent(in), dimension(:) :: Y,D,D2
      real(r64), intent(in), dimension(:,:) :: F
      real(r64), intent(out), dimension(:) :: A
      real(r64), intent(out), dimension(:,:) :: MY,MD,MD2
    end subroutine
  end interface

  real(r64) :: alpha = 0.1, eta = 0.66 * 0.1**0.33, nu = 0.5
  real(r64) :: omega, radius, facc, teff, zscale

  logical :: use_quench_mri = .false.
  logical :: use_prad_in_alpha = .true.
  logical :: use_flux_correction = .true.
  logical :: use_nu_times_ptot = .false.

  integer, parameter :: n_yout = 13
  integer, parameter :: c_rho = 1, c_temp = 2, c_trad = 3, &
      c_pgas = 4, c_prad = 5, c_pmag = 6, &
      c_frad = 7, c_fmag = 8, c_fcnd = 9, &
      c_ptot_gen = 10, c_heat = 11, c_vrise = 12, c_qmri = 13

contains

  !----------------------------------------------------------------------------!
  ! Generate model number based on its properties.

  pure function mrx_number(bil, magnetic, conduction) result(nr)
    logical, intent(in) :: magnetic,conduction
    character, intent(in) :: bil
    integer :: nr

    nr = 1

    select case (bil)
    case (EQUATION_DIFFUSION)
    case (EQUATION_COMPTON)
      nr = nr + 1
    case (EQUATION_BALANCE)
      nr = nr + 3
    case default
      error stop "wrong character for bil, allowed: D,W,C"
    end select

    if (magnetic)   nr = nr + 4
    if (conduction) nr = nr + 8

  end function

  !----------------------------------------------------------------------------!
  ! This function returns the number of: equations, boundary conditions etc..
  ! based on the code generated by sympy.

  elemental subroutine mrx_sel_dims (nr, ny, neq0, neq1, nbl, nbr)
    integer, intent(in) :: nr
    integer, intent(out) :: ny, neq1, nbl, nbr, neq0
    include 'mrxdims.fi'
  end subroutine

  !----------------------------------------------------------------------------!
  ! A wrapper to obtain only number of variables.

  elemental subroutine mrx_sel_nvar (nr, ny)
    integer, intent(in) :: nr
    integer, intent(out) :: ny
    integer :: neq1, nbl, nbr, neq0
    call mrx_sel_dims (nr, ny, neq0, neq1, nbl, nbr)
  end subroutine

  !----------------------------------------------------------------------------!
  ! Select the pointers to generated procedures that calculate the coefficients.

  subroutine mrx_sel_funcs (nr, feq0, feq1, fbl, fbr, fout)
    integer, intent(in) :: nr
    procedure(fun1_t), pointer, intent(out) :: feq1
    procedure(funout_t), pointer, intent(out) :: fout
    procedure(fun0_t), pointer, intent(out) :: fbl, fbr, feq0
    include 'mrxptrs.fi'
  end subroutine

  !----------------------------------------------------------------------------!
  ! Generate the column hash -- the point is that the column order in the model
  ! may change. Here in IHASH we have the column number of the respective field
  ! or zero if the given model does not contain it.
  ! order: (rho, T_gas, T_rad, F_rad, P_mag, F_cond)
  ! see variable yvar_all in coefficients.py

  pure subroutine mrx_sel_hash (nr, ihash)
    integer, intent(in) :: nr
    integer, dimension(:), intent(out) :: ihash
    include 'mrxhash.fi'
  end subroutine

  !----------------------------------------------------------------------------!
  ! Procedure to select the output function. In order to constistently evaluate
  ! some values (such as heating) and avoid errors, we also obtain them from
  ! code generated from symbolic computation.

  pure subroutine mrx_sel_fout (nr, fout)
    integer, intent(in) :: nr
    procedure(funout_t), pointer, intent(out) :: fout
    include 'mrxfout.fi'
  end subroutine

  !----------------------------------------------------------------------------!
  ! Generates the linear equation system that results in corrections to the
  ! vertical structure.

  subroutine mrx_matrix(nr,x,Y,M,A)

    integer :: nr
    real(r64), dimension(:), intent(in) :: x
    ! order: [ Y1(1) Y2(1) Y3(1) Y1(2) Y2(2) Y3(2) ... ]
    real(r64), dimension(:), intent(in) :: Y
    ! order: [ A1(1) A2(1) A3(1) A1(2) A2(2) A3(2) ... ]
    real(r64), dimension(:), intent(out) :: A
    real(r64), dimension(:,:), target, intent(out) :: M

    real(r64), dimension(:,:), allocatable :: MY, MD
    real(r64), dimension(:), allocatable :: YM,DY
    integer :: i, nx,nbl,nbr,ny,neq1,neq0
    procedure(fun1_t), pointer :: feq1
    procedure(fun0_t), pointer :: fbl, fbr, feq0
    procedure(funout_t), pointer :: fout
    ! opacities array: (derivative,type)
    ! order of types: abs, sct, cond
    real(r64), dimension(3,4) :: FV
    integer, dimension(6) :: c_

    nx = size(x)
    call mrx_sel_dims(nr,ny,neq0,neq1,nbl,nbr)
    allocate( ym(ny), dy(ny), MY(neq1,ny), MD(neq1,ny) )

    call mrx_sel_funcs(nr,feq0,feq1,fbl,fbr,fout)
    call mrx_sel_hash(nr,c_)

    if (nbl > 0) then
      associate (xbl => x(1), YBL => Y(1:ny),     &
              &  BL  => A(1:nbl),                 &
              &  MBL => M(1:nbl,1:ny))

        call kappabs(YBL(c_(1)), YBL(c_(2)), FV(1,1), FV(2,1), FV(3,1))
        call kappabp(YBL(c_(1)), YBL(c_(2)), FV(1,2), FV(2,2), FV(3,2))
        call kappesp(YBL(c_(1)), YBL(c_(2)), FV(1,3), FV(2,3), FV(3,3))
        call kappcnd(YBL(c_(1)), YBL(c_(2)), FV(1,4), FV(2,4), FV(3,4))

        call FBL(xbl, YBL, FV, BL, MBL)

      end associate
    end if

    if ( nbr > 0 ) then
      associate ( xbr => x(nx), YBR => Y((nx-1)*ny+1:nx*ny), &
                & BR  => A(nbl+(nx-1)*ny+1+neq0:nx*ny),           &
                & MBR => M(nbl+(nx-1)*ny+1+neq0:nx*ny, (nx-1)*ny+1:nx*ny))

        call kappabs(YBR(c_(1)), YBR(c_(2)), FV(1,1), FV(2,1), FV(3,1))
        call kappabp(YBR(c_(1)), YBR(c_(2)), FV(1,2), FV(2,2), FV(3,2))
        call kappesp(YBR(c_(1)), YBR(c_(2)), FV(1,3), FV(2,3), FV(3,3))
        call kappcnd(YBR(c_(1)), YBR(c_(2)), FV(1,4), FV(2,4), FV(3,4))

        call FBR(xbr, YBR, FV, BR, MBR)

      end associate
    end if

    if ( neq0 > 0 ) then
      fillc: do i = 1,nx

        associate ( xc => x(i), YC => Y((i-1)*ny+1:i*ny),    &
                  &  C => A(nbl+(i-1)*ny+1:nbl+(i-1)*ny+neq0), &
                  & MC => M(nbl+(i-1)*ny+1:nbl+(i-1)*ny+neq0, (i-1)*ny+1:i*ny))

          call kappabs(YC(c_(1)), YC(c_(2)), FV(1,1), FV(2,1), FV(3,1))
          call kappabp(YC(c_(1)), YC(c_(2)), FV(1,2), FV(2,2), FV(3,2))
          call kappesp(YC(c_(1)), YC(c_(2)), FV(1,3), FV(2,3), FV(3,3))
          call kappcnd(YC(c_(1)), YC(c_(2)), FV(1,4), FV(2,4), FV(3,4))

          call feq0(xc, YC, FV, C, MC)

        end associate
      end do fillc
    end if

    filla: do i = 1, nx-1

      associate ( dx => x(i+1) - x(i), xm => (x(i+1) + x(i)) / 2, &
              &   Ai => A(nbl+(i-1)*ny+1+neq0:nbl+i*ny),                 &
              &   M1 => M(nbl+(i-1)*ny+1+neq0:nbl+i*ny, (i-1)*ny+1:i*ny),  &
              &   M2 => M(nbl+(i-1)*ny+1+neq0:nbl+i*ny, i*ny+1:(i+1)*ny),  &
              &   Y1 => Y((i-1)*ny+1:i*ny), Y2 => Y(i*ny+1:(i+1)*ny))

        YM(:) = (Y2 + Y1) / 2
        DY(:) = (Y2 - Y1) / dx

        call kappabs(YM(c_(1)), YM(c_(2)), FV(1,1), FV(2,1), FV(3,1))
        call kappabp(YM(c_(1)), YM(c_(2)), FV(1,2), FV(2,2), FV(3,2))
        call kappesp(YM(c_(1)), YM(c_(2)), FV(1,3), FV(2,3), FV(3,3))
        call kappcnd(YM(c_(1)), YM(c_(2)), FV(1,4), FV(2,4), FV(3,4))

        call feq1(xm, YM, DY, FV, Ai, MY, MD)

        M1(:,:) = MY / 2 - MD / dx
        M2(:,:) = MY / 2 + MD / dx

      end associate

    end do filla

    deallocate(MY, MD, YM, DY)

  end subroutine

  !----------------------------------------------------------------------------!
  ! build a fake matrix for illustration

  subroutine mrx_fake_matrix(nx, ny, neq0, neq1, nbl, nbr, M)
    integer, intent(in) :: nx, ny, neq1, neq0, nbl, nbr
    integer, dimension(:,:), target, intent(out) :: M
    integer :: i

    if (size(M, 1) /= nx*ny .or. size(M,2) /= nx*ny) error stop
    if (neq0 + neq1 /= ny) error stop
    if (nbl + nbr /= neq1) error stop

    if (nbl > 0) then
      associate (MBL => M(1:nbl,1:ny))
        MBL(:,:) = 1
      end associate
    end if

    if ( nbr > 0 ) then
      associate (MBR => M(nbl+(nx-1)*ny+1+neq0:nx*ny, (nx-1)*ny+1:nx*ny))
        MBR(:,:) = 5
      end associate
    end if

    if ( neq0 > 0 ) then
      fillc: do i = 1, nx
        associate (MC => M(nbl+(i-1)*ny+1:nbl+(i-1)*ny+neq0, (i-1)*ny+1:i*ny))
          MC(:,:) = 2
        end associate
      end do fillc
    end if

    filla: do i = 1, nx - 1
      associate ( M1 => M(nbl+(i-1)*ny+1+neq0:nbl+i*ny, (i-1)*ny+1:i*ny),  &
              &   M2 => M(nbl+(i-1)*ny+1+neq0:nbl+i*ny, i*ny+1:(i+1)*ny))
        M1(:,:) = 3
        M2(:,:) = 4
      end associate
    end do filla
  end subroutine

  !----------------------------------------------------------------------------!
  ! Transfers vector Y between the models. Deletes columns if moving to lower
  ! model and fills them with guessed or default values if moving to more
  ! sophisticated model.

  subroutine mrx_transfer(nr_old, nr, z, Y)
    use slf_cgs, only: cgs_k_over_mh
    integer, intent(in) :: nr
    integer, intent(inout) :: nr_old
    real(r64), intent(in), dimension(:) :: z
    real(r64), intent(inout), dimension(:), allocatable, target :: Y
    real(r64), dimension(:), allocatable, target :: y_old
    real(r64), dimension(:,:), pointer :: yv, yv_old
    ! column order: rho, Tgas, Trad, Frad, Pmag, Fcond
    integer, dimension(6) :: c_, c_old_
    integer :: ny, ny_old

    call mrx_sel_hash(nr_old, c_old_)
    call mrx_sel_nvar(nr_old, ny_old)

    call mrx_sel_hash(nr, c_)
    call mrx_sel_nvar(nr, ny)

    call move_alloc(y, y_old)
    allocate(y(size(z)*ny))

    yv(1:ny,1:size(z)) => y
    yv_old(1:ny_old,1:size(z)) => y_old

    ! density, temperature and radiative flux are present in all models
    yv(c_(1),:) = yv_old(c_old_(1),:)
    yv(c_(2),:) = yv_old(c_old_(2),:)
    yv(c_(4),:) = yv_old(c_old_(4),:)

    ! if radiative and gas temperature are different in the new model,
    ! then set it equal to "diffusive" temperature
    if ( c_(2) .ne. c_(3) ) then
      yv(c_(3),:) = yv_old(c_old_(3),:)
    end if

    ! transfer magnetic pressure if present in new model
    if ( c_(5) .ne. 0 ) then
      ! if the old model also had it, just copy
      if ( c_old_(5) .ne. 0 ) then
        yv(c_(5),:) = yv_old(c_old_(5),:)
      else
        ! if not, set magnetic beta to constant value of 100
        yv(c_(5),:) = 0.01 * 2 * cgs_k_over_mh * yv(c_(1),:) * yv(c_(2),:)
      end if
    end if

    ! transfer thermal conduction flux if present in new model
    if ( c_(6) .ne. 0 ) then
      ! if the old model also had it, just copy
      if ( c_old_(6) .ne. 0 ) then
        yv(c_(6),:) = yv_old(c_old_(6),:)
      else
        ! if not, just set to zero
        yv(c_(6),:) = 0
      end if
    end if

    deallocate(y_old)
    nr_old = nr

  end subroutine

  !----------------------------------------------------------------------------!
  ! Allows to convert matrix to band format used by GBSV lapack procedure.

  subroutine mrx_bandim(nr, kl, ku)
    integer, intent(in) :: nr
    integer, intent(out) :: kl, ku
    integer :: ny, neq0, neq1, nbl, nbr

    call mrx_sel_dims(nr,ny,neq0,neq1,nbl,nbr)

    ku = 2 * (neq0 + neq1) - (1 + neq0 + nbl)
    kl = (neq0 + neq1) + nbl - 1
  end subroutine

  subroutine m2band(A,KL,KU,AB)
    real(r64), dimension(:,:), intent(in) :: A
    integer, intent(in) :: KL, KU
    real(r64), dimension(:,:), allocatable, intent(out) :: AB
    integer :: i,j,n

    if (size(A,1) /= size(A,2)) error stop "sizes must match!"

    n = size(A,1)

    if (allocated(AB)) then
      if ( size(AB,1) /= 2 * KL + KU + 1 .or. size(AB,2) /= N ) then
        deallocate(ab)
        allocate(AB(2 * KL + KU + 1, N))
      end if
    else
      allocate(AB(2 * KL + KU + 1, N))
    end if

    do j = 1, N
      do i = max(1, j - KU), min(N, j + KL)
        AB(KL + KU + 1 + i - j, j) = A(i, j)
      end do
    end do

  end subroutine m2band

  !----------------------------------------------------------------------------!
  ! Here all procedures generated by sympy are included.

  include 'mrxcoeff.fi'

end module relaxation

!------------------------------------------------------------------------------!
!------------------------ C BINDINGS FOR RELAXATION ---------------------------!
!------------------------------------------------------------------------------!

module relaxation_c

  use iso_c_binding
  use iso_fortran_env, only: r64 => real64
  use relaxation

  implicit none

contains

  subroutine mrx_number_c(cor, mag, cnd, nr) bind(C)
    integer(c_int), intent(in), value :: mag, cnd
    character(c_char), intent(in), value :: cor
    integer(c_int), intent(out) :: nr

    nr = mrx_number(cor, mag .ne. 0, cnd .ne. 0)
  end subroutine

  !----------------------------------------------------------------------------!

  subroutine mrx_get_ny_c(nr, ny) bind(C)
    integer(c_int), intent(in), value :: nr
    integer(c_int), intent(out) :: ny
    integer :: na, nc, nbl, nbr

    call mrx_sel_dims(nr, ny, nc, na, nbl, nbr)

  end subroutine

  !----------------------------------------------------------------------------!

  subroutine mrx_init_c(mb, md, r, alph, et, n) bind(C)
    real(c_double), intent(in), value :: mb, md, r, alph, et, n
    mbh = mb
    mdot = md
    radius = r
    alpha = alph
    nu = n
    eta = et
    call cylinder(mbh, mdot, radius, rschw, omega, facc, teff, zscale)
  end subroutine

  !----------------------------------------------------------------------------!

  subroutine mrx_matrix_c(nr,X,nx,Y,ny,A,na,M) bind(C)

    integer(c_int), intent(in), value :: nx,ny,na,nr
    real(c_double), intent(in), dimension(nx) :: X
    real(c_double), intent(in), dimension(nx*ny) :: Y
    real(c_double), intent(out), dimension(nx*na) :: A
    real(c_double), intent(out), dimension(nx*na,nx*ny) :: M

    call mrx_matrix(nr,x,Y,M,A)

  end subroutine

  !----------------------------------------------------------------------------!

  subroutine mrx_advance_c(nr,X,nx,Y,dY,ny,errno) bind(C)

    integer(c_int), intent(in), value :: nx,ny,nr
    integer(c_int), intent(out) :: errno
    real(c_double), intent(in), dimension(nx) :: X
    real(c_double), intent(in), dimension(nx*ny) :: Y
    real(c_double), intent(out), dimension(nx*ny) :: dY
    integer, dimension(nx*ny) :: ipiv
    real(r64) :: M(nx*ny,nx*ny)

    call mrx_matrix(nr, x, Y, M, dY)
    call dgesv(size(M,2), 1, M, size(M,1), ipiv, dY, size(dY), errno)

  end subroutine

end module
